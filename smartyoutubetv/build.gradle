apply from: '../../SharedUtils/constants.gradle'
buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // These docs use an open ended version so that our plugin
        // can be updated quickly in response to Android tooling updates

        // We recommend changing it to the latest version from our changelog:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'

// Put Fabric plugin after Android plugin
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    // FIX: Default interface methods are only supported starting with Android N (--min-api 24)
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion project.properties.compileSdkVersion
    buildToolsVersion project.properties.buildToolsVersion
    testOptions.unitTests.includeAndroidResources = true

    defaultConfig {
        minSdkVersion project.properties.minSdkVersion
        targetSdkVersion project.properties.targetSdkVersion
        applicationId "com.liskovsoft.videomanager"
        versionCode 1101
        versionName "6.17.521"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"

        multiDexEnabled = true
        // More info: http://myhexaville.com/2017/03/10/android-multidex/
        //multiDexKeepFile file('multidex-config.txt')
        //multiDexKeepProguard file('multidex-config.pro')
    }

    //signingConfigs {
    //    release {
    //        storeFile file("../android-upd2.keystore")
    //        storePassword project.KEYSTORE_PASSWORD
    //        keyAlias 'private'
    //        keyPassword project.KEYSTORE_PASSWORD
    //    }
    //}

    buildTypes {
        // NOTE: to enable Crashlytics add fabric.properties file with your api keys
        Boolean crashEnabled = file('fabric.properties').exists()

        debug {
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", crashEnabled.toString()
            ext.enableCrashlytics = crashEnabled
        }

        release {
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", crashEnabled.toString()
            ext.enableCrashlytics = crashEnabled

            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //signingConfig signingConfigs.release
        }
    }

    // gradle 4.6 migration: disable dimensions mechanism
    // more: https://proandroiddev.com/advanced-android-flavors-part-4-a-new-version-fc2ad80c01bb
    flavorDimensions "default"

    productFlavors {
        orig {
            matchingFallbacks = ['lite']
        }
        // Vtesting {
        //     matchingFallbacks = ['lite']
        //     applicationIdSuffix ".testing"
        // }
        Vkids {
            matchingFallbacks = ['Vkids', 'lite']
            applicationIdSuffix ".kids"
        }
        Vlive {
            matchingFallbacks = ['lite']
            applicationIdSuffix ".live"
        }
        // Vadnotam {
        //    matchingFallbacks = ['lite']
        //    applicationId "com.adnotam.smartyoutubetv"
        // }
        Vbolshoetv {
           // Eltex NV-501
           // vermax uhd250x/300x 1GB ram
           matchingFallbacks = ['lite']
           versionCode 9400 + defaultConfig.versionCode
        }
        Vbolshoetv2 {
            // Eltex NV-501
            matchingFallbacks = ['full']
            versionCode 9400 + defaultConfig.versionCode
            ndk {
                abiFilters "armeabi-v7a", ""
            }
        }
        // Vrtarmenia {
        //     matchingFallbacks = ['lite']
        //     versionCode 9400 + defaultConfig.versionCode
        //     applicationId "com.rtarmenia.smartyoutubetv"
        // }
        Vrtarmenia2 {
            matchingFallbacks = ['lite']
            versionCode 9400 + defaultConfig.versionCode
            applicationId "com.google.android.youtube.tv"
        }
        // Vunoiptv {
        //     matchingFallbacks = ['lite']
        //     versionCode 9400 + defaultConfig.versionCode
        //     applicationId "com.unoiptv.smartyoutubetv"
        // }
        Vredboxtv {
            matchingFallbacks = ['lite']
            versionCode 9400 + defaultConfig.versionCode
            applicationId "com.redboxtv.smartyoutubetv"
        }
    }

    // naming example: SmartYouTubeTV_Xwalk_v6.8.12_r.apk
    // https://stackoverflow.com/questions/18332474/how-to-set-versionname-in-apk-filename-using-gradle
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def project = "SYTV"
            def flavor = variant.productFlavors[-1].name.capitalize()
            def buildType = variant.variantData.variantConfiguration.buildType.name.take(1)
            def version = variant.versionName

            def newApkName = sprintf("%s_%s_v%s_%s.apk", [project, flavor, version, buildType])

            // gradle 4.6 migration
            output.outputFileName = new File(newApkName)
        }
    }

    lintOptions {
        abortOnError true
        lintConfig rootProject.file('lint.xml')
    }

    // FIX: Product Flavor: Duplicate class found.
    // Create common folder for duplicate classes.
    // More info: https://stackoverflow.com/questions/37439120/product-flavor-duplicate-class-found
    sourceSets {
        def commonDirs = ['src/commonSrc/appWatcher/java', 'src/commonSrc/crashHandler/java']
        def bolshoetvCommonDirs = ['src/commonSrc/Vbolshoetv/java']
        orig {
            java.srcDirs('src/commonSrc/crashHandler/java')
        }
        // Vtesting {
        //     java.srcDirs = commonDirs
        // }
        Vkids {
            java.srcDirs = commonDirs
        }
        Vlive {
            java.srcDirs = commonDirs
        }
        // Vrtarmenia {
        //     java.srcDirs = commonDirs
        // }
        Vrtarmenia2 {
            java.srcDirs = commonDirs
        }
        // Vunoiptv {
        //     java.srcDirs = commonDirs
        // }
        Vredboxtv {
            java.srcDirs = commonDirs
        }
        Vbolshoetv {
            java.srcDirs = bolshoetvCommonDirs
        }
        Vbolshoetv2 {
            java.srcDirs = bolshoetvCommonDirs
        }
    }
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:' + espressoCoreVersion, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:' + junitVersion
    // example: assertThat(newEmployees).hasSize(6).contains(frodo, sam);
    testImplementation 'org.assertj:assertj-core:' + assertJVersion
    testImplementation 'org.robolectric:robolectric:' + robolectricVersion
    //testImplementation project.properties.robolectricMultidexVersion
    //testImplementation project.properties.robolectricSupportV4Version
    testImplementation 'commons-io:commons-io:' + commonsIOVersion

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Common routines
    testImplementation project(path: ':sharedtests')
    implementation project(path: ':sharedutils')

    // ATV INSTALL_FAILED_CONFLICTING_PROVIDER fix
    origImplementation project(path: ':leanbackassistant')

    implementation project(path: ':common')

    implementation project(path: ':browser')

    implementation project(path: ':exoplayeractivity')

    implementation project(path: ':appupdatechecker')

    implementation('com.crashlytics.sdk.android:crashlytics:' + crashlyticsVersion) { // see top part of this file
        transitive = true
    }

    implementation 'androidx.appcompat:appcompat:' + appCompatXLibraryVersion
    implementation 'androidx.media:media:' + appCompatXLibraryVersion // exoplayer fix
    implementation 'androidx.recyclerview:recyclerview:' + appCompatXLibraryVersion // exoplayer fix
    implementation 'androidx.constraintlayout:constraintlayout:' + constraintLayoutXVersion

    implementation 'com.google.code.gson:gson:' + gsonVersion
    implementation 'com.jayway.jsonpath:json-path:' + jsonPathVersion

    implementation 'org.nanohttpd:nanohttpd:' + nanoHttpdVersion
    implementation 'com.algolia.instantsearch:voice:' + voiceOverlayVersion

    implementation 'com.squareup.okhttp3:okhttp:' + okhttpVersion
    implementation 'com.squareup:otto:' + ottoVersion

    implementation 'com.jakewharton:disklrucache:' + diskLruCacheVersion

    // https://mvnrepository.com/artifact/org.apache.commons
    implementation 'org.apache.commons:commons-lang3:' + apacheCommonsLangVersion
}
